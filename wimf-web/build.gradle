import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

apply plugin: 'com.moowork.node'
apply plugin: 'com.bmuschko.docker-remote-api'

node {
    version = '8.0.0'
    yarnVersion = '0.24.6'
    download = true
}

// avoids interactive test mode
task testWithCi(type: Exec) {
    dependsOn yarn_install
    environment 'CI', 'true'
    commandLine 'yarn', 'test'
}

// fails the build if there are linter errors
task buildWithCi(type: Exec) {
    dependsOn yarn_install, testWithCi, yarn_flow
    environment 'CI', 'true'
    commandLine 'yarn', 'build'
}

yarn_flow.dependsOn(yarn_install)
yarn_start.dependsOn(yarn_install)

task build(dependsOn: 'buildWithCi')
task run(dependsOn: 'yarn_start')
task test(dependsOn: ['testWithCi', 'yarn_flow'])
task check(dependsOn: test)

task createDockerfile(type: Dockerfile) {
    destFile = project.file('Dockerfile')
    from 'nginx:1'
    maintainer 'Rob Szewczyk <rob.szew@gmail.com>'
    addFile('build', '/usr/share/nginx/html')
    copyFile('nginx.conf', '/etc/nginx/conf.d/default.conf')
}

task dockerBuildImage(type: DockerBuildImage, dependsOn: [createDockerfile, buildWithCi]) {
    inputDir = project.file('.')
    tag = 'wimf/web'
}