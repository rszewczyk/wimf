import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

apply plugin: 'com.moowork.node'
apply plugin: 'com.bmuschko.docker-remote-api'

node {
    version = '8.0.0'
    yarnVersion = '0.24.6'
    download = true
}

yarn_build.dependsOn(yarn_install)
yarn_build.dependsOn(yarn_test)

yarn_test.dependsOn(yarn_flow)
yarn_test.dependsOn(yarn_install)

yarn_flow.dependsOn(yarn_install)

yarn_start.dependsOn(yarn_install)

task build(dependsOn: 'yarn_build')
task run(dependsOn: 'yarn_start')
task test(dependsOn: 'yarn_test')
task check(dependsOn: test)
task assemble(dependsOn: build)

task createDockerfile(type: Dockerfile) {
    destFile = project.file('Dockerfile')
    from 'nginx:1'
    maintainer 'Rob Szewczyk <rob.szew@gmail.com>'
    addFile('build', '/usr/share/nginx/html')
    copyFile('nginx.conf', '/etc/nginx/conf.d/default.conf')
}

task dockerBuildImage(type: DockerBuildImage, dependsOn: [createDockerfile, build]) {
    inputDir = project.file('.')
    tag = 'wimf/web'
}